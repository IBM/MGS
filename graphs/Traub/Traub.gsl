// =================================================================
// Licensed Materials - Property of IBM
//
// "Restricted Materials of IBM"
//
// BCM-YKT-07-18-2017
//
// (C) Copyright IBM Corp. 2005-2017  All rights reserved
//
// US Government Users Restricted Rights -
// Use, duplication or disclosure restricted by
// GSA ADP Schedule Contract with IBM Corp.
//
// =================================================================

#include "../std/std.gsl"
InitPhases = { init1, init2, initialize };
RuntimePhases = { dataCollect, run1, run2, lastPhase };
FinalPhases = { finalize };

#define DIRECTORY "./"
#define FILE_EXTENSION ".dat"
#define DELTA_T 0.0001


// ##### Driving wave input from cortex #####
// Node parameters
NodeType WaveDriverUnit(<
                        deltaT = DELTA_T,       // in s
                        op_constant = 0
                        >) {update->run1} ;

NodeType FileDriverUnit(<
                        deltaT = DELTA_T,       // in s
			sf = 0.001,
			inputFileName = "WT39_07_Delta_QuietRest_Ctx_zm_cutNeg_std_val1.txt",
                        op_random = 1,
                        scale = 0.8 //3.0
                        >) {initializeShared->init1, readInputFile->run1};

// Connectivity and weights
EachDstPropSrc ctx_2_fsi(RadialSampler(0.5));
BindName CTXw("weight", 1.0, "identifier", "driver");
NdplInAttrInit ctxW(CTXw);

// Initialize waves
#define AMPMIN 1.0
#define AMPMAX 1.5
BindName initWave("Hz", Gaussian(3.0,0.1),
                  "phase", Gaussian(0.0,6.28319), // in rad (360 = 6.28319)
                  "amplitude", UniformDist(AMPMIN, AMPMAX),
                  "wave", UniformDist(AMPMIN, AMPMAX));
NdplNodeInit heterWave(initWave);



// ##### FSI network #####
// Node parameters
NodeType TraubIAFUnit(< 
                      b = 10.0,                // s^-1      
                      GoC = 50.0,              // s^-1
                      C = 1.0,
                      k = {200.0, 20.0},       // s^-1
                      R = {0.0, 1.0},
                      E_L = -0.07,             // V
                      V_r = -0.07,             // V
                      Theta_r = -0.06,         // V
                      a = 0.0,                 // s^-1
                      A = {0.0, 0},            // V/s
                      s_tauR = 0.0008,         // s
                      s_tauF = 0.012,          // s
                      deltaT = DELTA_T,         // s
                      spike_cntMax = 0.001,    // s
                      np = 0,
                      V_max = 0.04,
                      sharedDirectory = DIRECTORY,
                      sharedFileExt = FILE_EXTENSION,
                      op_saveWeights = 1,
                      op_saveGJs = 1
                      >) {initializeShared->init2, outputPSPsShared->dataCollect, updateInput->run2, updateV->run1, threshold->run2};

// Synaptic connectivity
list<float> uniformDensity = {1.0};
EachDstPropSrc ipsp_fsi(RadialDensitySampler(72, 2.829, uniformDensity, 2.829));
// (nbrConnections, radius, densityDistribution, boundaryMargin)

// Synaptic weights 
BindName IPSP("weight", Neg(Scale(Exp(RangePassThru(-100,"<=",Gaussian(0.0,1.0),"<=",2.5)),0.75)), "identifier", "ipsp");
NdplInAttrInit ipsp(IPSP);

// Gap junctions connectivity
EachDstPropSrc gj_fsi(RadialDensitySampler(9, 1.415, uniformDensity, 1.415));

// Gap junctions weights
BindName GJ("conductance", 22.0, "identifier", "gj");
NdplInAttrInit gj(GJ);

// Initialize neurons
BindName initN("V", UniformDist(-0.07,-0.05), // Uniform from reset to mean threshold
               "Theta_inf", Neg(Gaussian(0.05,0.001))); // Gaussian around -0.05
NdplNodeInit heterN(initN);



// ##### Setup and connect the graph #####
Grid CTX
{
  Dimension(3,3,3);
  Layer(LayerV, WaveDriverUnit, UniformLayout(1), < nodekind="Nodes" >);
  InitNodes(.[].Layer(LayerV), heterWave);
  /*
  Layer(LayerV, FileDriverUnit, UniformLayout(1), < nodekind="Nodes" >);
  InitNodes(.[].Layer(LayerV), nodeDef);
  */
};

Grid FSI
{
  Dimension(27,27,27);
  Layer(FSIs, TraubIAFUnit, UniformLayout(1), < nodekind="Nodes" >);
  InitNodes(.[].Layer(FSIs), heterN);
  connectNodeSets(.[].Layer(FSIs), .[].Layer(FSIs),
                  ipsp_fsi, outAttrDef, ipsp);
  connectNodeSets(.[].Layer(FSIs), .[].Layer(FSIs),
                  gj_fsi, outAttrDef, gj);
};

Composite CortexFSI
{
  CTX ctx;
  FSI fsi;
  connectNodeSets(ctx[].Layer(LayerV), fsi[].Layer(FSIs),
                  ctx_2_fsi, outAttrDef, ctxW);
};

CortexFSI iaf;



// ##### Setup data collectors and analyzers #####
VariableType WaveDriverUnitDataCollector;
WaveDriverUnitDataCollector CTXWcollector<directory=DIRECTORY, fileExt=FILE_EXTENSION,
  op_saveWaves=1>;
polyConnect(iaf/ctx[].Layer(LayerV), CTXWcollector, <>, <>);
/*
VariableType FileDriverUnitDataCollector;
FileDriverUnitDataCollector CTXFcollector<directory=DIRECTORY, fileExt=FILE_EXTENSION,
  op_saveOutputs=1>;
polyConnect(iaf/ctx[].Layer(LayerV), CTXFcollector, <>, <>);
*/

VariableType TraubIAFUnitDataCollector;
TraubIAFUnitDataCollector FSIcollector<directory=DIRECTORY, fileExt=FILE_EXTENSION,
  op_saveSpikes=1, op_saveSpikesAll=0, Xmin=0, Xmax=8, Ymin=0, Ymax=8, Zmin=0, Zmin=8>;
polyConnect(iaf/fsi[].Layer(FSIs), FSIcollector, <>, <>);

VariableType LFPDataAnalyzer;
LFPDataAnalyzer LFPanalyzer<directory=DIRECTORY, fileExt=FILE_EXTENSION,
  elecRadius=13.27, elecSigma=2.65, numElecPerDimX=4, numElecPerDimY=4, numElecPerDimZ=4,
  op_saveLFPs=1>;
// LFP spacing and size is calculated as below
//        (informed by Einevoll et al, 2013, Nature;
//         Buzsaki et al., 2012, Nature Reviews Neuroscience):
// 1 unit grid = 113 um
// radius 1500 um / 113 um = 13.27 grid units
// sigma 300 um (middle of literature range of 200-400um) / 113 um = 2.65 grid units

polyConnect(iaf/fsi[].Layer(FSIs), LFPanalyzer, <>, <>);



// ##### Setup triggers #####
Trigger UnsignedTrigger(string description, Service svc, string operator,
                        int criterion, int delay);
Trigger CompositeTrigger(string description, Trigger triggerA, int critA,
                         string operator, Trigger triggerB, int critB, int delay);

UnsignedTrigger iterTrig("Iteration Trigger : >= 0 ", 
 			 ::Iteration, ">", 0, 0, dataCollect);

UnsignedTrigger iterTrigSampled("Iteration Trigger : !% 50", 
                                ::Iteration, "!%", 50, 0, dataCollect);

UnsignedTrigger endTrig("Iteration Trigger to end or stop", 
                        ::Iteration, "==", 30000, 0, lastPhase); // in dt
                     
CTXWcollector.dataCollection() on iterTrigSampled;                     
//CTXFcollector.dataCollection() on iterTrigSampled;
FSIcollector.dataCollectionSpikes() on iterTrig;
FSIcollector.dataCollectionOther() on iterTrigSampled;
LFPanalyzer.dataCollection() on iterTrigSampled;

Stop on endTrig;
