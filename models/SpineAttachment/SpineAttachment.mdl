#ifndef SpineAttachment_MDL
#define SpineAttachment_MDL

#include "../../nti/include/MaxComputeOrder.h"

#include "../std/std.mdl"
#include "../BranchSolver/BranchSolver.mdl"
#include "../HodgkinHuxley/HodgkinHuxley.mdl"

Interface SurfaceAreaLengthProducer {
	dyn_var_t* A; // [um^2]
	dyn_var_t* len; // [um]
}

// A SpineAttachment is a bidirectional connection between
//   a compartment in one neuron (serve as a spine neck)
//   to another compartment in another neuron (serve as dendritic compartment)
//Node SpineAttachment Implements CurrentProducer, BranchDataProducer, IndexProducer, 
//     VoltageProducer
Node SpineAttachment_Vm Implements CurrentProducer, BranchDataProducer, IndexProducer, 
     VoltageProducer,
     SurfaceAreaLengthProducer
{
/*{{{*/
	//{{{ need user-input
  dyn_var_t Raxial;   // [Gohm.um] 
  //}}}
  //{{{ internal calculation
    //either the 'i'-part or the 'j'-part will
    //  reference to 1. spine-neck
    //               2. den-shaft 
	//{{{ help finding the conductance 'g' as a function of necklength and neck-radius
  dyn_var_t *leni; //[um]
  dyn_var_t Ai;   //[um^2]
  dyn_var_t *lenj;
  dyn_var_t *Aj;
   //}}}
  dyn_var_t* Vi;  // [mV]
  dyn_var_t* Vj; 
  dyn_var_t I;   //the current [pA]
  dyn_var_t g;   //the conductance [nS] between 'neck' compartment to 'dendrite' compartment
  //}}}

  BranchDataStruct* branchData; // reference to the branch having the compartment of Vi
  int index;//index of the compartment that this SpineAttachment node associates with

  Shared {
	  dyn_var_t []* voltageConnect;
    DimensionStruct* []* dimensionsConnect;
  }

  InitPhase produceInitialState(Vi); // IMPORTANT: a treat - whenever a pointer to something else 
                       // is passed out via the Interface, make sure you pass it to the 
                       // InitPhase's method and RuntimePhase's method that synchronize data
  RuntimePhase produceState(Vi), computeState;

  // Output
  VoltageProducer.voltage << Vi;
  SurfaceAreaLengthProducer.A << &Ai;
  SurfaceAreaLengthProducer.len << leni;
  CurrentProducer.current << &I;
  BranchDataProducer.branchData << branchData;
  IndexProducer.index << &index;

  InAttrPSet {
    string identifier;
    int idx; //the index of the compartment in the compute-branch that connect to 
             // this SpineAttachment node
    string typeCpt; //"spine-neck" or "den-shaft"
  }

  UserFunction setVoltagePointers;
  UserFunction set_A_and_len;
  
  // Input (Connection setting)
  Connection Pre Node (PSet.identifier=="compartment[Voltage]") Expects 
    VoltageArrayProducer, BranchDataProducer, DimensionArrayProducer 
  {
    VoltageArrayProducer.voltageArray >> Shared.voltageConnect;
    DimensionArrayProducer.dimensionArray >> Shared.dimensionsConnect;   
    BranchDataProducer.branchData >> branchData;
    setVoltagePointers(); //set Vi
    set_A_and_len();//set Ai, leni
  }
  Connection Pre Node (PSet.identifier=="spineConnexon[Voltage]") Expects 
    VoltageProducer, SurfaceAreaLengthProducer
  {
	  VoltageProducer.voltage >> Vj;
	  SurfaceAreaLengthProducer.A >> Aj;
	  SurfaceAreaLengthProducer.len >> lenj;
  }
/*}}}*/
}

Node SpineAttachment_VmCai Implements CurrentProducer, BranchDataProducer, IndexProducer,
     VoltageProducer, 
     SurfaceAreaLengthProducer,
     CaConcentrationProducer, CaCurrentProducer
{
/*{{{*/
	//DATA
    /*{{{*/
	//{{{ need user-input
  dyn_var_t Raxial;   // [Gohm.um] 
  dyn_var_t RCacytoaxial;   // [Gohm.um] 
  //}}}
  //{{{ internal calculation
    //either the 'i'-part or the 'j'-part will
    //  reference to 1. spine-neck
    //               2. den-shaft 
	//{{{ help finding the conductance 'g' as a function of necklength and neck-radius
  dyn_var_t *leni; //[um]
  dyn_var_t Ai;   //[um^2]
  dyn_var_t *lenj;
  dyn_var_t *Aj;
   //}}}
  dyn_var_t* Vi;  // [mV]
  dyn_var_t* Vj; 
  dyn_var_t I;   //the current [pA]
  dyn_var_t g;   //the conductance [nS] between 'neck' compartment to 'dendrite' compartment
  dyn_var_t* Cai;
  dyn_var_t* Caj;
  dyn_var_t I_Ca;//the current [pA]  TUAN TOTHINK: should we use the fluxes???? J_Ca like RyR
  dyn_var_t gCYTO;
  //}}}
    /*}}}*/

  BranchDataStruct* branchData; // reference to the branch having the compartment of Vi
  int index;//index of the compartment that this SpineAttachment node associates with

  Shared {
    /*{{{*/
    dyn_var_t* T;
    dyn_var_t []* voltageConnect;
    dyn_var_t []* CaConcentrationConnect;
    DimensionStruct* []* dimensionsConnect;
    /*}}}*/
  }

  InitPhase produceInitialState(Vi, Cai);
  RuntimePhase produceState(Vi, Cai), computeState;

  //Output
  VoltageProducer.voltage << Vi;
  SurfaceAreaLengthProducer.A << &Ai;
  SurfaceAreaLengthProducer.len << leni;
  CurrentProducer.current << &I;
  CaConcentrationProducer.Ca << Cai;
  CaCurrentProducer.current << &I_Ca;
  BranchDataProducer.branchData << branchData;
  IndexProducer.index << &index;

  InAttrPSet {
    string identifier;
    int idx; //the index of the compartment in the compute-branch that connect to 
             // this SpineAttachment node
    string typeCpt; //"spine-neck" or "den-shaft"
  }

  UserFunction setVoltagePointers; 
  UserFunction setCaPointers;
  UserFunction set_A_and_len;
 
  // Input (Connection setting)
  Connection Pre Constant (PSet.identifier=="EC") Expects TemperatureProducer {
    TemperatureProducer.T >> Shared.T;
  }

  Connection Pre Node (PSet.identifier=="compartment[Voltage]") Expects 
    VoltageArrayProducer, BranchDataProducer, DimensionArrayProducer
  {
    VoltageArrayProducer.voltageArray >> Shared.voltageConnect;
    DimensionArrayProducer.dimensionArray >> Shared.dimensionsConnect;   
    BranchDataProducer.branchData >> branchData;
    setVoltagePointers(); //set Vi
    set_A_and_len();//set Ai, leni
  }
  Connection Pre Node (PSet.identifier=="spineConnexon[Voltage]") Expects 
    VoltageProducer, SurfaceAreaLengthProducer
  {
    VoltageProducer.voltage >> Vj;
    SurfaceAreaLengthProducer.A >> Aj;
    SurfaceAreaLengthProducer.len >> lenj;
  }

  Connection Pre Node (PSet.identifier=="compartment[Calcium]") Expects 
    CaConcentrationArrayProducer 
  {
    CaConcentrationArrayProducer.CaConcentrations >> Shared.CaConcentrationConnect;
    setCaPointers();
  }
  Connection Pre Node (PSet.identifier=="spineConnexon[Calcium]") Expects 
    CaConcentrationProducer 
  {
    CaConcentrationProducer.Ca >> Caj;
  }
/*}}}*/
}

Node SpineAttachment_VmCaiCaER Implements CurrentProducer, BranchDataProducer, IndexProducer,
     VoltageProducer, 
     SurfaceAreaLengthProducer,
     CaConcentrationProducer, CaCurrentProducer,
     CaERConcentrationProducer, CaERCurrentProducer
{
/*{{{*/
	//DATA
  /*{{{*/
	//{{{ need user-input
  dyn_var_t Raxial;   // [Gohm.um] 
  dyn_var_t RCacytoaxial;   // [Gohm.um] 
  dyn_var_t RCaERaxial;   // [Gohm.um] 
  //}}}
  //{{{ internal calculation
    //either the 'i'-part or the 'j'-part will
    //  reference to 1. spine-neck
    //               2. den-shaft 
	//{{{ help finding the conductance 'g' as a function of necklength and neck-radius
  dyn_var_t *leni; //[um]
  dyn_var_t Ai;   //[um^2]
  dyn_var_t *lenj;
  dyn_var_t *Aj;
   //}}}
  dyn_var_t* Vi;  // [mV]
  dyn_var_t* Vj; 
  dyn_var_t I;   //the current [pA]
  dyn_var_t g;   //the conductance [nS] between 'neck' compartment to 'dendrite' compartment
  dyn_var_t* Cai;
  dyn_var_t* Caj;
  dyn_var_t I_Ca;
  dyn_var_t gCYTO;
  dyn_var_t* CaERi;
  dyn_var_t* CaERj;
  dyn_var_t I_CaER;
  dyn_var_t gER; // the trans
  //}}}
  /*}}}*/

  BranchDataStruct* branchData; // reference to the branch having the compartment of Vi
  int index;//index of the compartment that this SpineAttachment node associates with

  Shared {
    /*{{{*/
    dyn_var_t* T;
    dyn_var_t []* voltageConnect;
    dyn_var_t []* CaConcentrationConnect;
    dyn_var_t []* CaERConcentrationConnect;
    DimensionStruct* []* dimensionsConnect;
    /*}}}*/
  }

  InitPhase produceInitialState(Vi, Cai, CaERi);
  RuntimePhase produceState(Vi, Cai, CaERi), computeState;

  VoltageProducer.voltage << Vi;
  SurfaceAreaLengthProducer.A << &Ai;
  SurfaceAreaLengthProducer.len << leni;
  CurrentProducer.current << &I;
  CaConcentrationProducer.Ca << Cai;
  CaCurrentProducer.current << &I_Ca;
  CaERConcentrationProducer.Ca << CaERi;
  CaERCurrentProducer.current << &I_CaER;

  BranchDataProducer.branchData << branchData;
  IndexProducer.index << &index;

  InAttrPSet {
    string identifier;
    int idx; //the index of the compartment in the compute-branch that connect to 
             // this SpineAttachment node
    string typeCpt; //help to identify the compartment: "spine-neck" or "den-shaft"
  }

  UserFunction setVoltagePointers; 
  UserFunction setCaPointers;
  UserFunction setCaERPointers;
  UserFunction set_A_and_len;
 
  Connection Pre Node (PSet.identifier=="compartment[Voltage]") Expects 
    VoltageArrayProducer, BranchDataProducer, DimensionArrayProducer
  {
    VoltageArrayProducer.voltageArray >> Shared.voltageConnect;
    DimensionArrayProducer.dimensionArray >> Shared.dimensionsConnect;   
    BranchDataProducer.branchData >> branchData;
    setVoltagePointers(); //set Vi
    set_A_and_len();//set Ai, leni
  }
  Connection Pre Node (PSet.identifier=="spineConnexon[Voltage]") Expects 
    VoltageProducer, SurfaceAreaLengthProducer
  {
    VoltageProducer.voltage >> Vj;
    SurfaceAreaLengthProducer.A >> Aj;
    SurfaceAreaLengthProducer.len >> lenj;
  }

  Connection Pre Node (PSet.identifier=="compartment[Calcium]") Expects 
    CaConcentrationArrayProducer 
  {
    CaConcentrationArrayProducer.CaConcentrations >> Shared.CaConcentrationConnect;
    setCaPointers();
  }

  Connection Pre Node (PSet.identifier=="spineConnexon[Calcium]") Expects 
    CaConcentrationProducer 
  {
    CaConcentrationProducer.Ca >> Caj;
  }

  //Connection Pre Node (PSet.identifier=="compartment[CalciumER]") Expects CaERConcentrationArrayProducer {
  Connection Pre Node (PSet.identifier=="compartment[CalciumER]") Expects 
    CaConcentrationArrayProducer 
  {
    CaConcentrationArrayProducer.CaConcentrations >> Shared.CaERConcentrationConnect;
    setCaERPointers();
  }

  Connection Pre Node (PSet.identifier=="spineConnexon[CalciumER]") Expects 
    CaERConcentrationProducer 
  {
    CaERConcentrationProducer.Ca >> CaERj;
  }

  Connection Pre Constant (PSet.identifier=="EC") Expects TemperatureProducer {
    TemperatureProducer.T >> Shared.T;
  }
/*}}}*/
}

#endif
