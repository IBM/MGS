// =================================================================
// Licensed Materials - Property of IBM
//
// "Restricted Materials of IBM"
//
// BCM-YKT-07-18-2017
//
// (C) Copyright IBM Corp. 2005-2017  All rights reserved
//
// US Government Users Restricted Rights -
// Use, duplication or disclosure restricted by
// GSA ADP Schedule Contract with IBM Corp.
//
// =================================================================

#ifndef BOUTONIAFUNIT_MDL
#define BOUTONIAFUNIT_MDL
#include "../PointNeuronTools/PointNeuronTools.mdl"

Node BoutonIAFUnit Implements GlutamateIAFProducer, AvailableGlutamateIAFProducer,
  CB1RIAFProducer, OutputProducer
{
  SpikeInput [] spikeInput;    // input of spike
  double glutamate;            // current output of glutamate
  double availableGlutamate;   // amount of glutamate available to be released
  double maxGlutamate;         // maximum amount of glutamate to recovery to

  ECBIAFInput [] ECBinput;     // input of endogenous cannabinoid
  double CB1R;                 // CB1R total quantity
  double CB1Runbound;          // CB1R unbound quantity
  double CB1Rrise;             // rise current of Cannabinoid receptor
  double CB1Rcurrent;          // fall current of Cannabinoid receptor

  GoodwinInput [] CB1input;    // input of CB1R protein levels
  
  Shared
    {
      double glutamateAdaptRate;    // Rate of surpression of glutamate release
      double glutamateRecoverTau;   // Rate of recovery from suppression

      double CB1RriseTau;           // rise time constant of CB1R
      double CB1RfallTau;           // fall time constant of CB1R

      double deltaT;                // in s

      string sharedDirectory;
      string sharedFileExt;

      InitPhase initializeShared;

      // Flags
      bool op_saveIndexs;
    }

  InAttrPSet
    {
      string identifier;
      double weight;           // potentially for structural plasticity
    }

  InitPhase initialize();
  RuntimePhase update(glutamate, availableGlutamate, CB1R, CB1Runbound, CB1Rcurrent);

  GlutamateIAFProducer.glutamate << &glutamate; // glutamate output
  AvailableGlutamateIAFProducer.availableGlutamate << &availableGlutamate; // available glutamate
  CB1RIAFProducer.CB1R << &CB1R; // CB1R total quantity
  CB1RIAFProducer.CB1Runbound << &CB1Runbound; // CB1R unbound quantity
  CB1RIAFProducer.CB1Rcurrent << &CB1Rcurrent; // CB1R current
  OutputProducer.output << &CB1Runbound; // CB1R unbound quantity for Goodwin model  
  
  UserFunction setSpikeIndices;
  UserFunction setECBIndices;

  Connection Pre Node (PSet.identifier=="inputSpikes") Expects SpikeProducer
  {
    SpikeProducer.spike >> spikeInput.spike;
    PSet.weight >> spikeInput.weight;
    setSpikeIndices();
  }

  Connection Pre Node (PSet.identifier=="ECBinput") Expects ECBIAFProducer
  {
    ECBIAFProducer.ECB >> ECBinput.ECB;
    PSet.weight >> ECBinput.weight;
    setECBIndices();
  }
  
  Connection Pre Node (PSet.identifier=="CB1input") Expects GoodwinProducer
  {
    GoodwinProducer.Y >> CB1input.Y; // only care about the amount of CB1, i.e. Y
    PSet.weight >> CB1input.weight;
    setECBIndices();
  }
}

#endif
