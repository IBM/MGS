#ifndef AMPAReceptor_MDL
#define AMPAReceptor_MDL

#include "../../nti/include/MaxComputeOrder.h"
#include "../std/std.mdl"
#include "../HodgkinHuxley/HodgkinHuxley.mdl"
#include "../BranchSolver/BranchSolver.mdl"

Node AMPAReceptor Implements ConductanceProducer, MaximumConductanceProducer, ReversalPotentialProducer, BranchDataArrayProducer, IndexArrayProducer {
  dyn_var_t g;
  dyn_var_t gbar;
  dyn_var_t r;

  dyn_var_t* V;
  dyn_var_t* w;
  int indexPost;
  int* [] indexPrePost;
 
  BranchDataStruct* [] branchDataPrePost;

  Shared {
    dyn_var_t* deltaT;
    dyn_var_t E;
    dyn_var_t alpha;
    dyn_var_t beta;
    dyn_var_t Tmax;
    dyn_var_t Vp;
    dyn_var_t Kp;
   }

  RuntimePhase updateAMPA;
  InitPhase initializeAMPA;

  ConductanceProducer.conductance << &g;
  MaximumConductanceProducer.maximumConductance << &gbar;
  ReversalPotentialProducer.reversalPotential << &Shared.E;
  BranchDataArrayProducer.branchDataArray << &branchDataPrePost;
  IndexArrayProducer.indexArray << &indexPrePost;

  InAttrPSet {
    string identifier;
    int idx;
  }

  UserFunction setPostIndex;

  Connection Pre Node (PSet.identifier=="preSynapticPoint") Expects VoltageProducer, BranchDataProducer, IndexProducer {
    VoltageProducer.voltage >> V;
    BranchDataProducer.branchData >> branchDataPrePost;
    IndexProducer.index >> indexPrePost;
  }

  Connection Pre Node (PSet.identifier=="compartment[Voltage]") Expects VoltageArrayProducer, BranchDataProducer {
    BranchDataProducer.branchData >> branchDataPrePost;
    setPostIndex();
  }

  Connection Pre Node (PSet.identifier=="receptor") Expects WeightProducer {
    WeightProducer.w >> w;
  }

  Connection Pre Constant (PSet.identifier=="dt") Expects TimeStepProducer {
    TimeStepProducer.deltaT >> Shared.deltaT;
  }
}

#endif
