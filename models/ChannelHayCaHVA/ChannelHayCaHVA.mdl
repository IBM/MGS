#ifndef ChannelHayCaHVA_MDL
#define ChannelHayCaHVA_MDL

#include "../std/std.mdl"
#include "../HodgkinHuxley/HodgkinHuxley.mdl"
#include "../BranchSolver/BranchSolver.mdl"

Node ChannelHayCaHVA Implements ConductanceArrayProducer, ReversalPotentialArrayProducer, CaCurrentArrayProducer, BranchDataProducer {
  dyn_var_t [] m;
  dyn_var_t [] h;
  dyn_var_t [] g;
  dyn_var_t [] gbar;
  dyn_var_t [] E_Ca;
  dyn_var_t [] I_Ca;

  dyn_var_t [] gbar_dists;
  dyn_var_t [] gbar_values;

  dyn_var_t []* V;
  dyn_var_t []* Ca_IC;

  BranchDataStruct* branchData; 
  DimensionStruct* []* dimensions;

  Shared {		
    dyn_var_t* Ca_EC;
    dyn_var_t* deltaT;
    dyn_var_t* T;
  }

  RuntimePhase update;	
  InitPhase initialize;

  ConductanceArrayProducer.conductanceArray << &g;
  ReversalPotentialArrayProducer.reversalPotentials << &E_Ca;
  CaCurrentArrayProducer.currents << &I_Ca;
  BranchDataProducer.branchData << branchData;

  InAttrPSet {
    string identifier;
    int idx;
  }

  Connection Pre Node (PSet.identifier=="compartment[Voltage]") Expects VoltageArrayProducer, BranchDataProducer, DimensionArrayProducer {
    VoltageArrayProducer.voltageArray >> V;
    BranchDataProducer.branchData >> branchData;
    DimensionArrayProducer.dimensionArray >> dimensions;
  }

  Connection Pre Node (PSet.identifier=="IC[Calcium]") Expects CaConcentrationArrayProducer {
    CaConcentrationArrayProducer.CaConcentrations >> Ca_IC;
  }

  Connection Pre Constant (PSet.identifier=="EC") 
	Expects CaConcentrationProducer, TemperatureProducer {
    CaConcentrationProducer.Ca >> Shared.Ca_EC;
    TemperatureProducer.T >> Shared.T;
  }

  Connection Pre Constant (PSet.identifier=="dt") Expects TimeStepProducer {
    TimeStepProducer.deltaT >> Shared.deltaT;
  }
}

#endif
