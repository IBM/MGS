#ifndef CaConcentration_MDL
#define CaConcentration_MDL

#include "../../nti/include/MaxComputeOrder.h"

#include "../std/std.mdl"
#include "../HodgkinHuxley/HodgkinHuxley.mdl"
#include "../BranchSolver/BranchSolver.mdl"
#include "../LaboratoryTools/LaboratoryTools.mdl"

Struct ChannelCaCurrents {
  dyn_var_t []* currents;
}

Struct ReceptorCaCurrent {
  dyn_var_t* current;
  int index;
}

Struct InjectedCaCurrent {
  dyn_var_t* current;
  dyn_var_t currentToConc;
  int index;
}

Node CaConcentration Implements SolutionArrayProducer, 
     DimensionArrayProducer, BranchDataProducer,
     ForwardSolutionArrayProducer, 
     CaConcentrationArrayProducer
{
/*{{{*/
  //DATA
    /*{{{*/
	//data members: reference
  DimensionStruct* [] dimensions;
  BranchDataStruct* branchData;
	//data members: internal, but for solver
	//NOTE: array data become ShallowArray<dyn_var_t> Aii in C++ code
  dyn_var_t [] Aii;
  dyn_var_t [] Aip;
  dyn_var_t [] Aim; // branch's distalmost compartment to junction coupling coefficient
  dyn_var_t [] RHS;
  dyn_var_t [] Aij;
  dyn_var_t* proximalCaConcentration;
  dyn_var_t* [] distalAiis;
  dyn_var_t* [] distalAips;
  dyn_var_t* [] distalInputs;
  DimensionStruct* [] distalDimensions;
  DimensionStruct* proximalDimension;
  bool proximalJunction;
  int computeOrder;
	//data members: internal
  dyn_var_t [] Ca_cur; // intracellular concentrations expressed by ion name alone
  dyn_var_t [] Ca_new;
  dyn_var_t [] currentToConc;
    // what is this for???
  dyn_var_t CaClearance;
    // currents
  ChannelCaCurrents [] channelCaCurrents;
  ReceptorCaCurrent [] receptorCaCurrents;
  InjectedCaCurrent [] injectedCaCurrents;
	/*}}}*/

   // design : anything currently not part of the spatial 
   //          is put in HodgkinHuxleyVoltage Node
  Shared {
    /*{{{*/
    dyn_var_t* deltaT;
    dyn_var_t DCa;
    dyn_var_t bmt;
    dyn_var_t beta;                    // Wagner and Keizer buffering constant
    dyn_var_t CaBaseline;

    InitPhase deriveParameters;  
    /*}}}*/
  }

	//output
	//{{{
  CaConcentrationArrayProducer.CaConcentrations << &Ca_new;
  SolutionArrayProducer.solutionArray << &Ca_new;
  DimensionArrayProducer.dimensionArray << &dimensions;
  BranchDataProducer.branchData << branchData;
  ForwardSolutionArrayProducer.AiiArray << &Aii;
  ForwardSolutionArrayProducer.AipArray << &Aip;
  ForwardSolutionArrayProducer.RHSArray << &RHS;
	//}}}

    // Phases methods
    /*{{{*/
  InitPhase initializeCaConcentration;

  #if MAX_COMPUTE_ORDER>0
  RuntimePhase forwardSolve1, backwardSolve1; 
  #endif
  #if MAX_COMPUTE_ORDER>1
  RuntimePhase forwardSolve2, backwardSolve2; 
  #endif
  #if MAX_COMPUTE_ORDER>2
  RuntimePhase forwardSolve3, backwardSolve3; 
  #endif
  #if MAX_COMPUTE_ORDER>3
  RuntimePhase forwardSolve4, backwardSolve4; 
  #endif
  #if MAX_COMPUTE_ORDER>4
  RuntimePhase forwardSolve5, backwardSolve5; 
  #endif
  #if MAX_COMPUTE_ORDER>5
  RuntimePhase forwardSolve6, backwardSolve6; 
  #endif
  #if MAX_COMPUTE_ORDER>6
  RuntimePhase forwardSolve7, backwardSolve7; 
  #endif

  RuntimePhase solve, finish;
    /*}}}*/

  InAttrPSet {	
    string identifier;
    TissueSite site;
    int idx;
  }

  UserFunction setReceptorCaCurrent, setInjectedCaCurrent, setProximalJunction;

  Connection Pre Constant (PSet.identifier=="dimension") Expects DimensionProducer {
    DimensionProducer.dimension >> dimensions;
   }

  Connection Pre Constant (PSet.identifier=="branchData") Expects BranchDataProducer {
    BranchDataProducer.branchData >> branchData;
   }

  Connection Pre Node (PSet.identifier=="channels[Calcium]") Expects CaCurrentArrayProducer {
    CaCurrentArrayProducer.CaCurrents >> channelCaCurrents.currents;
   }

  Connection Pre Node (PSet.identifier=="electricalSynapse[Calcium]") Expects CaCurrentProducer {
    CaCurrentProducer.CaCurrent >> injectedCaCurrents.current;
    setInjectedCaCurrent();
   }

  Connection Pre Node (PSet.identifier=="chemicalSynapse[Calcium]") Expects CaCurrentProducer {
    CaCurrentProducer.CaCurrent >> receptorCaCurrents.current;
    setReceptorCaCurrent();
   }

  PredicateFunction checkSite;
  Connection Pre Variable (PSet.identifier=="stimulation" && checkSite()) Expects CaCurrentProducer {
    CaCurrentProducer.CaCurrent >> injectedCaCurrents.current;
    setInjectedCaCurrent();
   }

  Connection Pre Node (PSet.identifier=="proximalJunctionPoint") Expects CaConcentrationProducer, DimensionProducer {
    CaConcentrationProducer.Ca >> proximalCaConcentration;
    DimensionProducer.dimension >> proximalDimension;
    setProximalJunction();
  }

  Connection Pre Node (PSet.identifier=="backwardSolvePoint") Expects SolutionProducer, DimensionProducer {
    SolutionProducer.solution >> proximalCaConcentration;
    DimensionProducer.dimension >> proximalDimension;
  }

  Connection Pre Node (PSet.identifier=="distalJunctionPoint") Expects CaConcentrationProducer, DimensionProducer {
    CaConcentrationProducer.Ca >> distalInputs;
    DimensionProducer.dimension >> distalDimensions;
  }

  Connection Pre Node (PSet.identifier=="forwardSolvePoint") Expects ForwardSolutionProducer, DimensionProducer {
    ForwardSolutionProducer.Aii >> distalAiis;
    ForwardSolutionProducer.Aip >> distalAips;
    ForwardSolutionProducer.RHS >> distalInputs;
    DimensionProducer.dimension >> distalDimensions;
  }

  PredicateFunction confirmUniqueDeltaT;
  Connection Pre Constant (PSet.identifier=="dt" && confirmUniqueDeltaT()) Expects TimeStepProducer {
    TimeStepProducer.deltaT >> Shared.deltaT;
  }
/*}}}*/
}

Node CaConcentrationEndPoint Implements CaConcentrationProducer, DimensionProducer
{
/*{{{*/
  dyn_var_t* CaConcentration;
  DimensionStruct* dimension;

  Shared {
    dyn_var_t []* CaConcentrationConnect;
    DimensionStruct* []* dimensionsConnect;
  }
  CaConcentrationProducer.Ca << CaConcentration;
  DimensionProducer.dimension << dimension;

  InAttrPSet {
    string identifier;
  }

  UserFunction setPointers;

  Connection Pre Node () Expects CaConcentrationArrayProducer, DimensionArrayProducer {
    CaConcentrationArrayProducer.CaConcentrations >> Shared.CaConcentrationConnect;
    DimensionArrayProducer.dimensionArray >> Shared.dimensionsConnect;   
    setPointers();
  }
  InitPhase produceInitialState(dimension);
  RuntimePhase produceSolvedCaConcentration(CaConcentration);
  RuntimePhase produceFinishedCaConcentration(CaConcentration);
/*}}}*/
}
#endif
