#ifndef NMDAReceptor_MDL
#define NMDAReceptor_MDL

#include "../std/std.mdl"
#include "../HodgkinHuxley/HodgkinHuxley.mdl"
#include "../BranchSolver/BranchSolver.mdl"

Node NMDAReceptor Implements ConductanceProducer, MaximumConductanceProducer, ReversalPotentialProducer, CaCurrentProducer, BranchDataArrayProducer, IndexArrayProducer, WeightProducer {
  float g;
  float gbar;
  float gbar0;
  float w;
  float buffer;
  float r;
  float I_Ca;
  float E_Ca;
  float tp;

  float* Vpre;
  float []* Vpost;
  float []* Ca_IC;
  int indexPost;
  int* [] indexPrePost;

  BranchDataStruct* [] branchDataPrePost;

  Shared {
    float alpha;
    float beta;
    float Tmax;
    float Vp;
    float Kp;
    float E;

    float* deltaT;
    float* T;
    float* Ca_EC;
    float* Mg_EC;
    float* Ketamine;

    //Plasticity params
    int plasticityOn;
    int plasticityStartAt;
    int plasticityStopAt;
    float theta_d;
    float theta_p;
    float gamma_d;
    float gamma_p;
    float tau;
    float w_th;

    //Meta-plasticity NMDAR
    float deltaNMDAR;
    float alphaBuffer;
    float tauBuffer;
   }

  RuntimePhase updateNMDA;
  RuntimePhase updateNMDADepPlasticity;
  InitPhase initializeNMDA;

  ConductanceProducer.conductance << &g;
  MaximumConductanceProducer.maximumConductance << &w;
  ReversalPotentialProducer.reversalPotential << &Shared.E;
  CaCurrentProducer.CaCurrent << &I_Ca;
  BranchDataArrayProducer.branchDataArray << &branchDataPrePost;
  IndexArrayProducer.indexArray << &indexPrePost;
  WeightProducer.w << &w; //Repeated in MaxConductanceProducer?

  InAttrPSet {
    string identifier;
    int idx;
  }

  UserFunction setPostIndex;

  Connection Pre Node (PSet.identifier=="preSynapticPoint") Expects VoltageProducer, BranchDataProducer, IndexProducer {
    VoltageProducer.voltage >> Vpre;
    BranchDataProducer.branchData >> branchDataPrePost;
    IndexProducer.index >> indexPrePost;
  }

  Connection Pre Node (PSet.identifier=="compartment[Voltage]") Expects VoltageArrayProducer, BranchDataProducer {
    VoltageArrayProducer.voltageArray >> Vpost;
    BranchDataProducer.branchData >> branchDataPrePost;
    setPostIndex();
  }

  Connection Pre Node (PSet.identifier=="IC[Calcium]") Expects CaConcentrationArrayProducer {
    CaConcentrationArrayProducer.CaConcentrations >> Ca_IC;
  }

  Connection Pre Constant (PSet.identifier=="EC") Expects CaConcentrationProducer, MgConcentrationProducer, TemperatureProducer, KetamineProducer {
    CaConcentrationProducer.Ca >> Shared.Ca_EC;
    MgConcentrationProducer.Mg >> Shared.Mg_EC;
    TemperatureProducer.T >> Shared.T;
    KetamineProducer.Ketamine >> Shared.Ketamine;
  }

  Connection Pre Constant (PSet.identifier=="dt") Expects TimeStepProducer {
    TimeStepProducer.deltaT >> Shared.deltaT;
  }

}

#endif
