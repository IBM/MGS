#ifndef NMDAReceptor_MDL
#define NMDAReceptor_MDL

#include "../../nti/include/MaxComputeOrder.h"
#include "../std/std.mdl"
#include "../HodgkinHuxley/HodgkinHuxley.mdl"
#include "../BranchSolver/BranchSolver.mdl"

Node NMDAReceptor Implements ConductanceProducer, MaximumConductanceProducer, 
            ReversalPotentialProducer, CaCurrentProducer, 
            BranchDataArrayProducer, IndexArrayProducer, WeightProducer 
{
/*{{{*/
	//DATA
	//{{{ 
	// Hodgkin-Huxley-based formula 
	//internal data
	//{{{
  dyn_var_t g;     // [nS/um^2]
  dyn_var_t gbar;  // [nS/um^2]
  dyn_var_t gbar0;
  dyn_var_t w;     // [unitless] - plasticity factor (if enabled)
  dyn_var_t buffer; //[nS/ms]  - METAplasticity factor (if enabled)
  dyn_var_t r; // fraction of opening receptors
  dyn_var_t I_Ca; // [pA/um^2]
  dyn_var_t E_Ca; // [mV]
  dyn_var_t tp; // TUAN: why we have 'tp' here?
  //}}}

  //reference data
  //{{{only use with preSynapticPoint
  dyn_var_t* Vpre;// [mV] preSynaptic voltage
  //}}}
  dyn_var_t* Glut; //[uM] concentration
  dyn_var_t []* Vpost;
  dyn_var_t []* Ca_IC; // [uM]
  int indexPost; //index of compartment in post-synaptic side (i.e. spine head)
  int* [] indexPrePost; // index of compartment in pre-synaptic side (i.e. bouton)

  BranchDataStruct* [] branchDataPrePost;
  //}}}

  Shared {
    //{{{ 
     //NT + C <==>[alpha][beta] O.NT
    dyn_var_t alpha; // uM^-1 msec^-1
    dyn_var_t beta;  // msec^-1
		//{{{ only used with preSynapticPoint
    dyn_var_t NTmax; // [uM] maximum concentration of NeuroTransmitter Glutamate
    dyn_var_t Vp; // [mV]  voltage at which concentration of Glutamate is at half-peak
    dyn_var_t Kp; // [mV]  the steepness of the curve of Glutamate = f(Vm)
	  //}}}
    dyn_var_t E;

    dyn_var_t* deltaT;
    dyn_var_t* T;
		dyn_var_t Tadj;  // [unitless] kinetics adjustment based on recorded data 
		//   and tempt. used for the simulation 
		InitPhase computeTadj;  
    dyn_var_t* Mg_EC; // [mM]
    dyn_var_t* Ketamine; // 0..1 (fraction of Ketamine block)
    dyn_var_t* Ca_EC; // [uM]
    dyn_var_t* Glycine; // [uM]

    //Plasticity params
    int plasticityOn;
    int plasticityStartAt;
    int plasticityStopAt;
    dyn_var_t theta_d; // threshold of Ca2+ for depression
    dyn_var_t theta_p; // threshold of Ca2+ for potentiation
    dyn_var_t gamma_d;
    dyn_var_t gamma_p;
    dyn_var_t tau;
    dyn_var_t w_th;

    //Meta-plasticity NMDAR
    dyn_var_t deltaNMDAR;
    dyn_var_t alphaBuffer;
    dyn_var_t tauBuffer;
    //}}}
   }

  InitPhase initializeNMDA;
  RuntimePhase updateNMDA;
  RuntimePhase updateNMDADepPlasticity;

  // Output
	//{{{
  ConductanceProducer.conductance << &g;
  MaximumConductanceProducer.maximumConductance << &w;
  ReversalPotentialProducer.reversalPotential << &Shared.E;
  CaCurrentProducer.current << &I_Ca;
  BranchDataArrayProducer.branchDataArray << &branchDataPrePost;
  IndexArrayProducer.indexArray << &indexPrePost;
  WeightProducer.w << &w; //Repeated in MaxConductanceProducer?
  //}}}

  InAttrPSet {//information of the incoming connection
    string identifier; 
    int idx;   // index of the compartment on a branch
          // if identifier is the name of the compartment variable
  }

  UserFunction setPostIndex;

  // Connection (input)
	//{{{
  Connection Pre Node (PSet.identifier=="preSynapticPoint") Expects VoltageProducer, 
     BranchDataProducer, IndexProducer {//the info from presynaptic-side is passed to receptor via this
      // intermediate nodetype
		//{{{
    VoltageProducer.voltage >> Vpre;
    BranchDataProducer.branchData >> branchDataPrePost;
    IndexProducer.index >> indexPrePost;
    //}}}
  }

  Connection Pre Node (PSet.identifier=="synapticCleft") Expects GlutamateConcentrationProducer, 
     BranchDataProducer, IndexProducer {
    GlutamateConcentrationProducer.NT >> Glut;
    BranchDataProducer.branchData >> branchDataPrePost;
    IndexProducer.index >> indexPrePost;
  }

  Connection Pre Node (PSet.identifier=="compartment[Voltage]") Expects VoltageArrayProducer, 
     BranchDataProducer {//the info from postsynaptic-side is passed to receptor directly
    //REMEMBER: When use in C++ code, only use Vpost[indexPost]
    VoltageArrayProducer.voltageArray >> Vpost;
    BranchDataProducer.branchData >> branchDataPrePost;
    setPostIndex(); // so that Vpost[indexPost] point to the voltage in the proper compartment index 
  }

  Connection Pre Node (PSet.identifier=="IC[Calcium]") Expects CaConcentrationArrayProducer {
    //REMEMBER: When use in C++ code, only use Ca_IC[indexPost]
    CaConcentrationArrayProducer.CaConcentrations >> Ca_IC;
  }

  Connection Pre Constant (PSet.identifier=="EC") Expects CaConcentrationProducer, 
        MgConcentrationProducer, TemperatureProducer, KetamineProducer, GlycineProducer 
  {
		//{{{
    CaConcentrationProducer.Ca >> Shared.Ca_EC;
    MgConcentrationProducer.Mg >> Shared.Mg_EC;
    TemperatureProducer.T >> Shared.T;
    KetamineProducer.Ketamine >> Shared.Ketamine;
    GlycineProducer.Glycine >> Shared.Glycine;
    //}}}
  }

  Connection Pre Constant (PSet.identifier=="dt") Expects TimeStepProducer {
    TimeStepProducer.deltaT >> Shared.deltaT;
  }
  //}}}

/*}}}*/
}

#endif
