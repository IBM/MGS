// =============================================================================
// (C) Copyright IBM Corp. 2005-2025. All rights reserved.
//
// Distributed under the terms of the Apache License
// Version 2.0, January 2004.
// (See accompanying file LICENSE or copy at http://www.apache.org/licenses/.)
//
// =============================================================================

#ifndef KCaChannel_MDL
#define KCaChannel_MDL

#include "../../nti/include/MaxComputeOrder.h"

#include "../std/std.mdl"
#include "../HodgkinHuxley/HodgkinHuxley.mdl"
#include "../BranchSolver/BranchSolver.mdl"

Node KCaChannel Implements ConductanceArrayProducer, ReversalPotentialArrayProducer, BranchDataProducer {
  dyn_var_t [] s;	
  dyn_var_t [] g;
  dyn_var_t [] gbar;

  dyn_var_t []* Ca;
 
  BranchDataStruct* branchData; 

  Shared {
    dyn_var_t* K_IC;
    dyn_var_t* K_EC;
    dyn_var_t* deltaT;
    dyn_var_t* T;
    dyn_var_t [] E_K;
    InitPhase computeE_K;
  }

  RuntimePhase update;
  InitPhase initializeKCaChannels;

  ConductanceArrayProducer.conductanceArray << &g;
  ReversalPotentialArrayProducer.reversalPotentials << &Shared.E_K;
  BranchDataProducer.branchData << branchData;

  InAttrPSet {
    string identifier;
    int idx;
  }

  Connection Pre Node (PSet.identifier=="compartment[Calcium]") Expects CaConcentrationArrayProducer, BranchDataProducer {
    CaConcentrationArrayProducer.CaConcentrations >> Ca;
    BranchDataProducer.branchData >> branchData;
  }

  Connection Pre Node (PSet.identifier=="IC[Voltage]") Expects KConcentrationProducer {
    KConcentrationProducer.K >> Shared.K_IC;
  }

  Connection Pre Constant (PSet.identifier=="EC") 
    Expects KConcentrationProducer, TemperatureProducer {
    KConcentrationProducer.K >> Shared.K_EC;
    TemperatureProducer.T >> Shared.T;
  }

  Connection Pre Constant (PSet.identifier=="dt") Expects TimeStepProducer {
    TimeStepProducer.deltaT >> Shared.deltaT;
  }
}

#endif
